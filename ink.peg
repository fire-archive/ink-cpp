# ? (option, aka zero-or-one)
# lpeg ^-1
# * (aka Kleene star, zero-or-more) 
# lpeg ^0
# + (one-or-more)
# lpeg ^1
# a b (with spaces between them) is a 'sequence' expression
# lpeg *
# a / b is an 'ordered choice' expression
# lpeg +
# https://github.com/PhilippeSigaud/Pegged/wiki/PEG-Basics
# Ported from https://github.com/premek/pink/blob/master/pink/parser.lua

ROOT <- LINES
LINES <- LINE*
LINE <- STMT / GATHER / PARA
STMT <- GLUE / DIVERT / KNOT / STITCH / OPTION / COMM

OPTION <- OPT_STARS OPT_ANS _

OPT_DIV_CONT <- (!']' .)*
OPT_ANS_WITH_DIV <- _ '[' _ OPT_DIV_CONT _ ']' / OPT_ANS_CONT _
OPT_ANS_WITHOUT_DIV <- _
OPT_ANS <- OPT_ANS_WITH_DIV / OPT_ANS_WITHOUT_DIV
OPT_ANS_CONT <- (![\r\n] .)*

OPT_STARS <- OPT_STAR OPT_STAR* OPT_ANS OPT_CONT _
OPT_STAR <- _ '*'
OPT_CONT <- (![\r\n] .)*

GATHER <- GATHER_MARKS
GATHER_MARKS <- GATHER_MARK GATHER_MARK* GATHER_CONT _
GATHER_MARK <- _ '-'
GATHER_CONT <- (![\r\n] .)*

GLUE <- _ '<>' _

DIVERT <- DIVERT_END / DIVERT_JUMP

DIVERT_SYM <- '->' _
DIVERT_END <- DIVERT_SYM 'END' _
DIVERT_JUMP <- DIVERT_SYM _ ADDR _

KNOT <- ('=' '='+ ) _ ID _ ('=')* _
STITCH <- '=' _ ID _ ('=')* _

PARA <- (!STMT .)*
COMM <- COMM_OL / COMM_ML / TODO
COMM_CONT <- ( ![\r\n] .)*
TODO <- _ 'TODO:' _ COMM_CONT _
COMM_OL <- _ '//' _ COMM_CONT _
COMM_ML <- _ '/*' _ COMM_ML_CONT '*/'
COMM_ML_CONT <- (!'*/' .)*
ADDR <- ID ('.' ID)?
ID <- ([a-zA-Z]+ / '_') ([a-zA-Z0-9] / '_')*
~_ <- [ \t\r\n]*
EOF <- !.